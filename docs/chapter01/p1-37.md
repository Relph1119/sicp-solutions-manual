# 练习1.37
&emsp;&emsp;a）一个无穷连分式是一个如下形式的表达式：$$
f=\frac{N_1}{\displaystyle D_1+\frac{N_2}{\displaystyle D_2+\frac{N_3}{D_3+\cdots}}}$$&emsp;&emsp;作为一个例子，我们可以证明在所有的$N_i$和$D_i$都等于1时，这一无穷连分式产生出$1/\phi$，其中的$\phi$就是黄金分割率（见1.2.2节的描述）。逼近某个无穷连分式的一种方是在给定数目的项之后截断，这样的一个截断称为$k$项有限连分式，其形式是：$$
\frac{N_1}{\displaystyle D_1+\frac{N_2}{\displaystyle \ddots+\frac{N_K}{D_K}}}$$&emsp;&emsp;假定$n$和$d$都是只有一个参数（项的下标$i$）的过程，它们分别返回连分式的项$N_i$和$D_i$。请定义一个过程`cont-frac`，使得对`(cont-frac n d k)`的求值，计算出$k$项有限连分式的值。通过如下调用检查你的过程对于顺序的$k$值是否逼近$1 / \phi$：
```lisp
(cont-frac (lambda (i) 1.0)
           (lambda (i) 1.0)
           k)
```
&emsp;&emsp;你需要取多大的$k$才能保证得到的近似值具有十进制的4位精度？  
&emsp;&emsp;b）如果你的过程产生一个递归计算过程，那么请写出另一个产生迭代计算的过程。如果它产生迭代计算，请写出另一个过程，使之产生一个递归计算过程。

**解答：**  
（1）递归计算过程，代码见`src\practices\ch01\p1-37-rec-cont-frac.scm`
&emsp;&emsp;根据题目给出的$k$项有限连分式$$
\frac{N_1}{\displaystyle D_1+\frac{N_2}{\ddots+\frac{N_K}{D_K}}}$$可得到递归式$$
f(i) = \left\{ \begin{array}{ll}
\displaystyle \frac{N_i}{D_i + f(i+1)} & i<k\\
\displaystyle \frac{N_k}{D_k} & i=k
\end{array} \right.$$

```lisp
(define (cont-frac N D k)
    (define (cf i)
        (if (= k i)
            (/ (N k) (D k))
            (/ (N i)
               (+ (D i) (cf (+ i 1))))))
    (cf 1))
```
&emsp;&emsp;用递归连分式定义黄金分割率函数，代码见`src\practices\ch01\p1-37-golden-ratio.scm`
```
(load "src/practices/ch01/p1-37-rec-cont-frac.scm")

(define (golden-ratio k)
    (+ 1
       (cont-frac (lambda (i) 1.0)
                  (lambda (i) 1.0)
                  k)))
```
&emsp;&emsp;执行并测试
```shell
PS > scheme "src\practices\ch01\p1-37-golden-ratio.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (golden-ratio 1)
2.0
> (golden-ratio 10)
1.6179775280898876
> (golden-ratio 11)
1.6180555555555556
> (golden-ratio 12)
1.6180257510729614
>
```
&emsp;&emsp;观察可知，当$k>10$能保证得到的近似值具有十进制的4位精度。

（2）迭代计算过程，代码见`src\practices\ch01\p1-37-iter-cont-frac.scm`
```lisp
(define (cont-frac N D k)
    (define (iter i result)
        (if (= i 0)
            result
            (iter (- i 1) (/ (N i)
                             (+ (D i) result)))))
    (iter (- k 1)
          (/ (N k) (D k))))
```
用迭代连分式定义黄金分割率函数并测试
```lisp
(load "src/practices/ch01/p1-37-iter-cont-frac.scm")

(define (golden-ratio k)
    (+ 1
       (cont-frac (lambda (i) 1.0)
                  (lambda (i) 1.0)
                  k)))
```
```shell
PS > scheme "src\practices\ch01\p1-37-golden-ratio.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (golden-ratio 10)
1.6179775280898876
> (golden-ratio 11)
1.6180555555555556
> (golden-ratio 12)
1.6180257510729614
> 
```