# 练习1.32
&emsp;&emsp;a）请说明，`sum`和`product`（练习1.31）都是另一称为`accumulate`的更一般概念的特殊情况，`accumulate`使用某些一般性的累积函数组合起来的一系列项：
```lisp
(accumulate combiner null-value term a next b)
```
&emsp;&emsp;`accumulate`取的是与`sum`和`product`一样的项和范围描述参数，再加上一个（两个参数的）`combiner`过程，它描述如何将当前项与前面各项的积累组合起来，另外还有一个`null-value`参数，它描述再所有的项都用完时的基本值。请写出`accumulate`，并说明我们能怎样基于简单地调用`accumulate`，定义出`sum`和`product`来。  
&emsp;&emsp;b）如果你的`accumulate`过程生成的是一个递归计算过程，那么请写出一个生成迭代计算过程的过程。如果它生成一个迭代计算过程，请写出一个生成递归计算过程的过程。  

**解答：**  
（1）递归`accumulate`函数  
&emsp;&emsp;根据题意，我们需要写出一个`accumulate`函数，代码见`src\practices\ch01\p1-32-rec-accumulate.scm`
```lisp
(define (accumulate combiner null-value term a next b)
    (if (> a b)
        null-value
        (combiner (term a)
                  (accumulate combiner null-value term (next a) next b))))
```
（2）迭代`accumulate`函数，代码见`src\practices\ch01\p1-32-iter-accumulate.scm`
```lisp
(define (accumulate combiner null-value term a next b)
    (define (iter a result)
        (if (> a b)
            result
            (iter (next a)
                  (combiner result (term a)))))
    (iter a null-value))
```
执行并测试
```shell
PS > scheme "src\practices\ch01\p1-32-iter-accumulate.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (define (term a) a)
> (define (next a) (+ a 1))
> (accumulate + 0 term 1 next 10)
55
> 
```
（3）重定义`sum`函数，代码见`src\practices\ch01\p1-32-sum.scm`
```lisp
(load "src/practices/ch01/p1-32-rec-accumulate.scm")

(define (sum term a next b)
    (accumulate + 0 term a next b))
```
执行并测试
```shell
PS > scheme "src\practices\ch01\p1-32-sum.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (define (term x) x)
> (define (next x) (+ x 1))
> (sum term 1 next 10)
55
>
```
（4）重定义`product`，代码见`src\practices\ch01\p1-32-product.scm`
```lisp
(load "src/practices/ch01/p1-32-rec-accumulate.scm")

(define (product term a next b)
    (accumulate * 1 term a next b))
```
执行并测试
```shell
PS > scheme "src\practices\ch01\p1-32-product.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (define (term x) x)
> (define (next x) (+ x 1))
> (product term 1 next 10)
3628800
>
```