# 练习1.19
&emsp;&emsp;存在着一种以对数步数求出斐波那契数的巧妙算法。请回忆1.2.2节`fib-iter`计算过程中状态变量`a`和`b`的变换规则，$a \leftarrow a+b$和$b \leftarrow a$，现在将这种变换称为$T$变换。通过观察可以发现，从1和0开始将T反复应用$n$次，将产生出一对数`Fib(n+1)`和`Fib(n)`。换句话说，斐波那契数可以通过将$T^n$（变换$T$的$n$次方）应用于对偶$(1,0)$而产生出来。现在将$T$看作是变换族$T_{pq}$中$p=0$且$q=1$的特殊情况，其中$T_{pq}$是对于对偶$(a,b)$按照$a \leftarrow bq+aq+ap$和$b \leftarrow bp+aq$规则的变换。  
&emsp;&emsp;请证明，如果应用变换$T_{pq}$两次，其效果等同于应用同样形式的一次变换$T_{p'q'}$，其中$p'$和$q'$可以由$p$和$q$计算出来。这就指明了一条求出这种变换的平方的路径，使我们可以通过连续求平方的方式去计算$T^n$，就像`fast-expt`过程里所做的那样。将所有这些集中到一起，就形成了下面的过程，其运行只需要对数的步数：  
```lisp
(define (fib n)
    (fib-iter 1 0 0 1 n))

(define (fib-iter a b p q count)
    (cond ((= count 0) b)
          ((even? count) 
           (fib-iter a 
                     b 
                     <??> ;compute p'
                     <??> ;compute q'
                     (/ count 2)))
          (else (fib-iter (+ (* b q) (* a q) (* a p))
                          (+ (* b p) (* a q))
                          p
                          q
                          (- count 1)))))
```

**解答：**  
&emsp;&emsp;根据题意，已知对于对偶$(a,b)$，有$$T_{pq}=\left\{ 
\begin{array}{ll}
a & \leftarrow bq+a(p+q) \\
b & \leftarrow bp + aq
\end{array}
\right.$$  
&emsp;&emsp;故可计算$T_{pq}$的平方，有$$T_{pq}^2=\left\{
\begin{array}{ll}
a & \leftarrow (bp+aq)q+(bq + a(p+q))(p+q)=b(2pq+q^2)+a(p^2+q^2+2pq+q^2) \\
b & \leftarrow (bp+aq)p+(bq+a(p+q))q=b(p^2+q^2)+a(2pq+q^2) 
\end{array}
\right.$$  
&emsp;&emsp;通过上式，可得变换$T_{p'q'}$，其中$p'=p^2+q^2,q'=2pq+q^2$。  
&emsp;&emsp;当$n$为偶数时，可通过变换$T_{p'q'}$从而降低计算$T^n$所需的计算量，从而得到对数级复杂度的斐波那契数计算过程，代码见`src\practices\ch01\p1-19.scm`：  
```lisp
(load "src/examples/ch01/p8-square.scm")

(define (fib n)
    (fib-iter 1 0 0 1 n))

(define (fib-iter a b p q count)
    (cond ((= count 0) b)
          ((even? count) 
           (fib-iter a 
                     b 
                     (+ (square p) (square q)) ;compute p'
                     (+ (* 2 p q) (square q)) ;compute q'
                     (/ count 2)))
          (else (fib-iter (+ (* b q) (* a q) (* a p))
                          (+ (* b p) (* a q))
                          p
                          q
                          (- count 1)))))
```
&emsp;&emsp;执行并测试：  
```shell
PS > scheme 
> (load "src/practices/ch01/p1-19.scm")
> (fib 3)
2
> (fib 5)
5
> (fib 8)
21
```