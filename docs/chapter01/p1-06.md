# 练习1.6
&emsp;&emsp;Alyssa P. Hacker看不出为什么需要将`if`提供为一种特殊形式，她问：“为什么我不能直接通过`cond`将它定义为一个常规过程呢？”Alyssa的朋友Eva Lu Ator断言确实可以这样做，并定义了`if`的一个新版本：  
```lisp
(define (new-if predicate then-clause else-clause)
    (cond (predicate then-clause)
		  (else else-clause)))
```
Eva给Alyssa演示她的程序：
```lisp
(new-if (= 2 3) 0 5)
5
(new-if (= 1 1) 0 5)
0
```
她很高兴地用自己的`new-if`重写了求平方根的程序：
```lisp
(define (sqrt-iter guess x)
	(new-if (good-enough? guess x)
		     guess
			 (sqrt-iter (improve guess x)
			 			x)))
```
当Alyssa试着用这个过程去计算平方根时会发生什么事情呢？请给出解释。  

**解答：**  
&emsp;&emsp;见代码`src\parctices\ch01\p1-6.scm`  
&emsp;&emsp;执行该程序，程序一直陷入循环中，造成死循环，添加`(trace sqrt-iter)`，就会发现它执行了大量的递归调用，这些调用数量非常庞大，以至于造成死循环。  
```shell
PS > scheme "src\examples\ch01\p16-sqrt.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

|(sqrt-iter 1.0 9)
| (sqrt-iter 5.0 9)
| |(sqrt-iter 3.4 9)
| | (sqrt-iter 3.023529411764706 9)  
| | |(sqrt-iter 3.00009155413138 9)  
| | | (sqrt-iter 3.000000001396984 9)
| | | |(sqrt-iter 3.0 9)
| | | | (sqrt-iter 3.0 9)
| | | | |(sqrt-iter 3.0 9)
| | | | | (sqrt-iter 3.0 9)
| | | |[10](sqrt-iter 3.0 9)
| | | |[11](sqrt-iter 3.0 9)
| | | |[12](sqrt-iter 3.0 9)
```
&emsp;&emsp;可知，造成`sqrt-iter`函数报错的原因，一定是新定义的`new-if`函数。  
&emsp;&emsp;通过书中第12页，`if`语句是条件表达式的一种受限形式。
```lisp
(if <predicate> <consequent> <alternative>)
```
&emsp;&emsp;如果`<predicate>`得到真值，就求值`<consequent>`并返回其值，否则求值`<alternative>`并返回其值。  
&emsp;&emsp;由于新定义的`new-if`只是一个普通函数，根据解释器所使用的模式是应用序求值，每个函数的实际参数在传入的时候都会被求值，所以，当使用`new-if`函数时，无论`predicate`为真值还是假值，`then-clause`和`else-clause`两个分支都会被求值。  
可以通过以下实验进行验证`if`和`new-if`的区别，如果使用`if`，代码只会打印good：  
```shell
> (if #t (display "good") (display "bad"))
good
> 
```
&emsp;&emsp;如果使用`new-if`，两个语句都会被打印：
```shell
> (new-if #t (display "good") (display "bad"))
badgood
>
```
&emsp;&emsp;关于打印`badgood`的顺序，由于解释器MIT Scheme对参数的求值顺序是从右往左的原则，故先执行`(display "bad")`后执行`(display "good")`。