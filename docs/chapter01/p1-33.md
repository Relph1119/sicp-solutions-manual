# 练习1.33
&emsp;&emsp;你可以通过引进一个处理被组合项的过滤器（filter）概念，写出一个比`accumulate`（练习1.32）更一般的版本。也就是说，在计算过程中，只组合起由给定范围得到的项里的那些满足特定条件的项。这样得到的`filtered-accumulate`抽象取与上面累积过程同样的参数，再加上一个另外的描述有关过滤器的谓词参数。请写出`filterd-accumulate`作为一个过程，说明如何用`filtered-accumulate`表达以下内容：  
a）求出在区间$a$到$b$中所有素数之和（假定你已经写出了谓词`prime?`）。  
b）小于$n$的所有与$n$互素的正整数（即所有满足$\text{GCD}(i,n)=1$的整数$i<n$）之乘积。  

**解答：**  
（1）写出`filterd-accumulate`函数，代码见`src\practices\ch01\p1-33-filterd-accumulate.scm`
```lisp
(define (filtered-accumulate combiner null-value term a next b valid?)
    (if (> a b)
        null-value
        (let ((rest-terms (filtered-accumulate combiner null-value term (next a) next b valid?)))
            (if (valid? a)
                (combiner (term a) rest-terms)
                rest-terms))))
```
（2）计算素数之和，代码见`src\practices\ch01\p1-33-filtered-accumulate.scm`
```lisp
(load "src/practices/ch01/p1-33-filtered-accumulate.scm")
(load "src/examples/ch01/p33-prime.scm")

(define (primes-sum a b)
    (define (term x) x)
    (define (next x) (+ x 1))
    (filtered-accumulate + 0 term a next b prime?))
```  
&emsp;&emsp;执行并测试
```shell
PS > scheme "src\practices\ch01\p1-33-prime-sum.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (primes-sum 1 10)
18
> 
```
（3）计算互素正整数之乘积  
&emsp;&emsp;根据题中给出的互素性质（即所有满足$\text{GCD}(i,n)=1$的整数$i<n$），使用`gcd`函数写出`coprime?`过程，用于检查两个整数是否互素，代码见`src\practices\ch01\p1-33-coprime.scm`
```lisp
(load "src/examples/ch01/p32-gcd.scm")

(define (coprime? a b)
    (and (< a b)
         (= 1 (gcd a b))))
```
&emsp;&emsp;编写`product-of-coprimes`函数，代码见`src\practices\ch01\p1-33-product-of-coprimes.scm`
```lisp
(load "src/practices/ch01/p1-33-coprime.scm")
(load "src/practices/ch01/p1-33-filtered-accumulate.scm")

(define (product-of-coprimes n)
    (define (term x) x)
    (define (next x) (+ x 1))
    (define (valid? x) (coprime? x n))
    (filtered-accumulate * 1 term 1 next n valid?))
```
&emsp;&emsp;执行并测试
```shell
PS > scheme "src\practices\ch01\p1-33-product-of-coprimes.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (product-of-coprimes 10)
189
> 
```
（4）迭代`filtered-accumulate`函数，代码见`src\practices\ch01\p1-33-iter-filtered-accumulate.scm`
```lisp
(define (filtered-accumulate combiner null-value term a next b valid?)
    (define (iter i result)
        (cond ((> i b) result)
              ((valid? i) (iter (next i) (combiner result (term i))))
              (else (iter (next i) result))))
    (iter a null-value))
```
&emsp;&emsp;执行并测试
```shell
PS > scheme "src\practices\ch01\p1-33-iter-accumulate-product.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (define (term x) x)
> (define (next x) (+ x 1))
> (filtered-accumulate + 0 term 1 next 10 even?)
30
>
```