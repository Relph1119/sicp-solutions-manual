# 练习1.43
&emsp;&emsp;如果$f$是一个数值函数，$n$是一个正整数，那么我们可以构造出$f$的$n$次重复应用，将其定义为一个函数，这个函数在$x$的值是$f(f(\cdots(f(x))\cdots))$。举例说，如果$f$是函数$x \mapsto x+1$，$n$次重复应用$f$就是函数$x \mapsto x+n$。如果$f$是求一个数的平方的操作，$n$次重复应用$f$就是求出其参数的$2^n$次幂。  
&emsp;&emsp;请写一个过程，它的输入是一个计算$f$的过程和一个正整数$n$，返回的是能计算$f$的$n$次重复应用的那个函数。你的过程应该能以如下方式使用：
```lisp
((repeated square 2) 5)
625
```
&emsp;&emsp;提示：你可能发现使用练习1.42的`compose`能带来一些方便。

**解答：**
（1）不使用`compose`的递归计算过程，代码见`src\practices\ch01\p1-43-rec-repeated.scm`
```lisp
(define (repeated f n)
    (if (= n 1)
        f
        (lambda (x)
            (f ((repeated f (- n 1)) x)))))
```
（2）不使用`compose`的迭代计算过程，代码见`src\practices\ch01\p1-43-iter-repeated.scm`
```lisp
(define (repeated f n)
    (define (iter i repeated-f)
        (if (= i 1)
            repeated-f
            (iter (- i 1)
                (lambda (x) (f (repeated-f x))))))
    (iter n f))
```
测试上面两个函数
```shell
PS > scheme "src\practices\ch01\p1-43-iter-repeated.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (define (square x) (* x x))
> ((repeated square 2) 5)
625
> 
```
（3）使用`compose`的递归计算过程，代码见`src\practices\ch01\p1-43-rec-compose-repeated.scm`
```lisp
(load "src/practices/ch01/p1-42-compose.scm")

(define (repeated f n)
    (if (= n 1)
        f
        (compose f (repeated f (- n 1)))))
```
（4）使用`compose`的迭代计算过程，代码见`src\practices\ch01\p1-43-iter-compose-repeated.scm`
```lisp
(load "src/practices/ch01/p1-42-compose.scm")

(define (repeated f n)
    (define (iter i repeated-f)
        (if (= i 1)
            repeated-f
            (iter (- i 1)
                (compose f repeated-f))))    
    (iter n f))
```