# 练习1.46
&emsp;&emsp;本章描述的一些数值算法都是迭代式改进的实例。迭代式改进是一种非常具有一般性的计算策略，它说的是：为了计算出某些东西，我们可以从对答案的某个初始猜测开始，检查这一猜测是否足够好，如果不行就改进这一猜测，将改进之后的猜测作为新的猜测去继续这一计算过程。  
&emsp;&emsp;请写一个过程`iterative-improve`，它以两个过程为参数：其中之一表示告知某个猜测是否足够好的方法，另一个表示改进猜测的方法。`interactive-improve`的返回值应该是一个过程，它以某一个猜测为参数，通过不断改进，直至得到的猜测足够好为止。利用`iterative-improve`重写1.1.7节的`sqrt`过程和1.3.3节的`fixed-point`过程。

**解答：**  
&emsp;&emsp;根据题意，需要编写一个`iterative-improve`函数，其中包括两个入参，一个表示告知某个猜测是否足够好的方法`close-enough?`，另一个表示改进猜测的方法`imporve`，返回值为一个单参数（初始猜测值`first-guess`）函数。  
根据上述描述，该函数的形式如下：
```lisp
(define (iterative-improve close-enough? imporve)
    (lambda (first-guess)
        <??>
    ))
```
&emsp;&emsp;本题所要编写的函数和1.3.3节`fixed-point`过程非常相似：
```lisp
(define tolerance 0.00001)

(define (fixed-point f first-guess)
    (define (close-enough? v1 v2)
        (< (abs (- v1 v2)) tolerance))
    (define (try guess)
        (let ((next (f guess)))
            (if (close-enough? guess next)
                next
                (try next))))
    (try first-guess))
```
&emsp;&emsp;首先将`close-enough?`函数抽出
```lisp
(define tolerance 0.00001)

(define (close-enough? v1 v2)
        (< (abs (- v1 v2)) tolerance))

(define (fixed-point f first-guess close-enough?)
    (define (try guess)
        (let ((next (f guess)))
            (if (close-enough? guess next)
                next
                (try next))))
    (try first-guess))
```
&emsp;&emsp;再将`first-guess`从`fixed-point`参数列表中抽走，变成另一个封装了`try`函数的`lambda`的参数
```lisp
(define tolerance 0.00001)

(define (close-enough? v1 v2)
        (< (abs (- v1 v2)) tolerance))

(define (fixed-point f close-enough?)
    (lambda (first-guess)
        (define (try guess)
            (let ((next (f guess)))
                (if (close-enough? guess next)
                    next
                    (try next))))
    (try first-guess)))
```
&emsp;&emsp;现在`fixed-point`过程和`iterative-improve`过程定义非常相似了，可直接进行变换
```lisp
(define (iterative-improve close-enough? improve)
    (lambda (first-guess)
        (define (try guess)
            (let ((next (improve guess)))
                (if (close-enough? guess next)
                    next
                    (try next))))
    (try first-guess)))
```

（1）用`iterative-improve`重定义`fixed-point`，代码见`src\practices\ch01\p1-46-fixed-point.scm`
```lisp
(load "src/practices/ch01/p1-46-iterative-improve.scm")

(define (fixed-point f first-guess)
    (define tolerance 0.00001)
    (define (close-enough? v1 v2)
        (< (abs (- v1 v2)) tolerance))
    (define (improve guess)
        (f guess))
    ((iterative-improve close-enough? improve) first-guess))
```
&emsp;&emsp;执行并测试
```shell
PS > scheme "src\practices\ch01\p1-46-fixed-point.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (fixed-point cos 1.0)
0.7390822985224023
>
```

（2）用`iterative`重定义`sqrt`，代码见`src\practices\ch01\p1-46-sqrt.scm`
```lisp
(load "src/practices/ch01/p1-46-iterative-improve.scm")

(define (sqrt x)
    (define dx 0.00001)
    (define (close-enough? v1 v2)
        (< (abs (- v1 v2)) dx))
    (define (improve guess)
        (define (average x y)
            (/ (+ x y) 2))
        (average guess (/ x guess)))
    ((iterative-improve close-enough? improve) 1.0))
```
&emsp;&emsp;执行并测试
```shell
PS > scheme "src\practices\ch01\p1-46-sqrt.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (sqrt 4)
2.000000000000002
> (sqrt 9)
3.0
>
```