# 练习1.29
&emsp;&emsp;辛普森规则是另一种比上面所用规则更精确的数值积分方法。采用辛普森规则，函数$f$在范围$a$和$b$之间的定积分的近似值是$$\frac{h}{3}[y_0 + 4y_1 + 2y_2 + 4y_3 + 2y_4 + \cdots + 2y_{n-2} + 4y_{n-1} + y_n]$$&emsp;&emsp;其中$h=(b-a)/n$，$n$是某个偶数，而$y_k=f(a+kh)$（增大$n$能提高近似值的精度）。请定义一个具有参数$f$、$a$、$b$和$n$，采用辛普森规则计算并返回积分值的过程。用你的函数求出cube在0和1之间的积分（用$n=100$和$n=1000$），并将得到的值与上面用`integral`过程所得到的结果比较。  

**解答：**  
&emsp;&emsp;根据题意，思路如下：  
- 计算$h=(b-a)/n$
- 计算$y_k=f(a+kh)$
- 计算$y_k$的系数
- 编写`simpson`函数

（1）计算$h=(b-a)/n$  
```lisp
(define h (/ (- b a) n))
```
（2）计算$y_k=f(a+kh)$  
```lisp
(define (y k) (f (+ a (* k h))))
```
（3）计算$y_k$的系数    
&emsp;&emsp;根据题意，根据下标$k$的不同，$y_k$有三个不同的系数：  
- 当$k=0$或$k=n$时，系数为1
- 当$k$为奇数时，系数为4
- 当$k$为偶数时，系数为2

```lisp
(define (factor k)
    (cond ((or (= k 0) (= k n))
           1)
          ((odd? k) 4)
          (else 2)))
```
（4）编写`simpson`函数，代码见`src\practices\ch01\p1-29-simpson.scm`
```lisp
(load "src/examples/ch01/p38-sum.scm")

(define (simpson f a b n)
    (define h (/ (- b a) n))
    (define (y k) (f (+ a (* k h))))
    (define (factor k)
        (cond ((or (= k 0) (= k n)) 1)
            ((odd? k) 4)
            (else 2)))
    (define (term k)
        (* (factor k) (y k)))
    (define (next k)
        (+ k 1))
    (if (not (even? n))
        (error "n can't be odd")
        (* (/ h 3)
           (sum term (exact->inexact 0) next n))))
```
&emsp;&emsp;执行并测试
```shell
PS > scheme "src\practices\ch01\p1-29-simpson.scm"
Chez Scheme Version 9.5
Copyright 1984-2017 Cisco Systems, Inc.

> (define (cube x) (* x x x))
> (simpson cube 0 1 100)
0.24999999999999992
> (simpson cube 0 1 1000)
0.2500000000000003
>
```